ТипДокументаЗаказа = "ДокументСсылка.ЗаказПоставщику";
ТипДокументаПоступления = "ДокументСсылка.ПоступлениеТоваровУслуг";
ТипДокументаСчетФактуры = "ДокументСсылка.СчетФактураПолученный";
ТипДокументаВозвратПоставщику = "ДокументСсылка.ВозвратТоваровПоставщику";
ИмяДокументаВозвратПоставщику = "ВозвратТоваровПоставщику";

ИмяОрганизации = "Организация";
ИмяКонтрагента = "Контрагент";
ИмяТочкиДоставки = "Склад";
ИмяТочкиДоставкиВПоступлении = "СкладОрдер";
ИмяДатаПоставки = "ДатаПоступления";
НомерВходящегоДокумента = "НомерВходящегоДокумента";
ДатаВходящегоДокумента = "ДатаВходящегоДокумента";
ИмяЗаказа1С = "Сделка";
ОснованиеВозвратаПоставщику = "Сделка";

// Заказ поставщику
Если ТипЗнч(Источник.Ссылка) = Тип(ТипДокументаЗаказа) Тогда
    
    ВыборкаУчастниковВЭДО = КонтрагентУчаствуетВЭДО(Источник[ИмяОрганизации], Источник[ИмяКонтрагента]);
    Если НЕ ВыборкаУчастниковВЭДО.Количество() = 0 Тогда
        
        ВыборкаУчастниковВЭДО.Следующий();
        
        НаборЗаписей = РегистрыСведений.ЭКОМ_ЦепочкиДокументовПокупателя.СоздатьНаборЗаписей();
        НаборЗаписей.Отбор.Заказ.Установить(Источник.Ссылка); // поле проиндексировано
        
        ДанныеИзмерений = Новый Структура;
        ДанныеИзмерений.Вставить("Заказ", Источник.Ссылка);
        ДанныеИзмерений.Вставить("ORDERDATE", Источник.Дата);
        ДанныеИзмерений.Вставить("ORDERNUMBER", Источник.Номер);
        ДанныеИзмерений.Вставить("Организация", Источник[ИмяОрганизации]);
        ДанныеИзмерений.Вставить("Контрагент", Источник[ИмяКонтрагента]);
        ДанныеИзмерений.Вставить("ТочкаДоставки", Источник[ИмяТочкиДоставки]);
        
        ДанныеРесурсов = Новый Структура;
        ДанныеРесурсов.Вставить("xmlОрганизация", ВыборкаУчастниковВЭДО.GLN_Организация);
        ДанныеРесурсов.Вставить("xmlКонтрагент", ВыборкаУчастниковВЭДО.GLN_Контрагент);
        ДанныеРесурсов.Вставить("xmlТочкаДоставки", ВыборкаУчастниковВЭДО.GLN_ТочкаДоставки);
        ДанныеРесурсов.Вставить("ДатаПоставки", Источник[ИмяДатаПоставки]);
        ДанныеРесурсов.Вставить("КоличествоЗаказ", Источник.Товары.Итог("Количество"));
        ДанныеРесурсов.Вставить("СуммаЗаказ", Источник.Товары.Итог("Сумма"));
        
        // Выполним блокировку данных
        ЭКОМ_ПодпискаСобытий.ВыполнитьБлокировкуНабораЗаписей(НаборЗаписей.Отбор, "ЭКОМ_ЦепочкиДокументовПокупателя");
        
        НаборЗаписей.Прочитать();
        Если НаборЗаписей.Количество() = 0 Тогда
            // Записываем документ в первый раз
            ЭКОМ_ПодпискаСобытий.СформироватьЗаписьВРегистр(НаборЗаписей.Добавить(), ДанныеИзмерений, ДанныеРесурсов); // записываем данные в регистр если они заполнены
            
        Иначе
            // Если существует документ в регистре
            Для Каждого НоваяЗапись Из НаборЗаписей Цикл
                ЭКОМ_ПодпискаСобытий.СформироватьЗаписьВРегистр(НоваяЗапись, ДанныеИзмерений, ДанныеРесурсов); // записываем данные в регистр если они заполнены
            КонецЦикла;
            
        КонецЕсли;
        
        НаборЗаписей.Записать();
        
    КонецЕсли;
    
КонецЕсли;

// Поступление
Если ТипЗнч(Источник.Ссылка) = Тип(ТипДокументаПоступления) Тогда
    
    ВыборкаУчастниковВЭДО = КонтрагентУчаствуетВЭДО(Источник[ИмяОрганизации], Источник[ИмяКонтрагента]);
    Если НЕ ВыборкаУчастниковВЭДО.Количество() = 0 Тогда
        
        ДокументЗаказа = Неопределено;
        Если ЗначениеЗаполнено(Источник[ИмяЗаказа1С]) Тогда
            ДокументЗаказа = Источник[ИмяЗаказа1С];
        КонецЕсли;
        
        НаборЗаписей = РегистрыСведений.ЭКОМ_ЦепочкиДокументовПокупателя.СоздатьНаборЗаписей();
        
        ЗаписьНайдена = Ложь;
        
        // 1. Ищем по заказу
        Если ЗначениеЗаполнено(ДокументЗаказа) Тогда
            НаборЗаписей.Отбор.Заказ.Установить(ДокументЗаказа); // поле проиндексировано
            
            // Выполним блокировку данных
            ЭКОМ_ПодпискаСобытий.ВыполнитьБлокировкуНабораЗаписей(НаборЗаписей.Отбор, "ЭКОМ_ЦепочкиДокументовПокупателя");
            
            НаборЗаписей.Прочитать();
            Если НЕ НаборЗаписей.Количество() = 0 Тогда
                ЗаписьНайдена = Истина;
            КонецЕсли;
        КонецЕсли;
        
        // 2. Поиск по дате и номеру десадва
        Если ЗаписьНайдена = Ложь Тогда
            
            DESADV = Неопределено;
            Запрос = Новый Запрос;
            
            Запрос.Текст =
                "ВЫБРАТЬ
                |   ЭКОМ_ЦепочкиДокументовПокупателя.DESADV,
                |   ЭКОМ_ЦепочкиДокументовПокупателя.Заказ
                |ИЗ
                |   РегистрСведений.ЭКОМ_ЦепочкиДокументовПокупателя КАК ЭКОМ_ЦепочкиДокументовПокупателя
                |ГДЕ
                |   ЭКОМ_ЦепочкиДокументовПокупателя.DESADV.НакладнаяНомер = &НакладнаяНомер
                |   И ЭКОМ_ЦепочкиДокументовПокупателя.DESADV.НакладнаяДата = &НакладнаяДата";
            
            Запрос.УстановитьПараметр("НакладнаяДата", Источник[ДатаВходящегоДокумента]);
            Запрос.УстановитьПараметр("НакладнаяНомер", Источник[НомерВходящегоДокумента]);
            РезультатЗапроса = Запрос.Выполнить();
            Если НЕ РезультатЗапроса.Пустой() Тогда
                Выборка = РезультатЗапроса.Выбрать();
                Выборка.Следующий();
                
                ДокументЗаказа = Выборка.Заказ;
                DESADV = Выборка.ЭКОМ_ДокументDESADV;
            КонецЕсли;
            
            
            ДанныеОтбораЗаполнены = Ложь;
            НаборЗаписей = РегистрыСведений.ЭКОМ_ЦепочкиДокументовПокупателя.СоздатьНаборЗаписей();
            
            СтруктураОтбора = Новый Структура;
            СтруктураОтбора.Вставить("Заказ", ДокументЗаказа);
            СтруктураОтбора.Вставить("DESADV", DESADV);
            Для каждого Данные Из СтруктураОтбора Цикл
                Если ЗначениеЗаполнено(Данные.Значение) Тогда
                    НаборЗаписей.Отбор[Данные.Ключ].Установить(Данные.Значение);
                    ДанныеОтбораЗаполнены = Истина;
                КонецЕсли;
            КонецЦикла;
            
            // Выполним блокировку данных
            ЭКОМ_ПодпискаСобытий.ВыполнитьБлокировкуНабораЗаписей(НаборЗаписей.Отбор, "ЭКОМ_ЦепочкиДокументовПокупателя");
            Если ДанныеОтбораЗаполнены Тогда
                НаборЗаписей.Прочитать();
                Если НЕ НаборЗаписей.Количество() = 0 Тогда
                    ЗаписьНайдена = Истина;
                КонецЕсли;
            КонецЕсли;
            
        КонецЕсли;
        
        Если ЗаписьНайдена Тогда
            
            ДанныеИзмерений = Новый Структура;
            ДанныеИзмерений.Вставить("Накладная", Источник.Ссылка);
            ДанныеИзмерений.Вставить("Организация", Источник[ИмяОрганизации]);
            ДанныеИзмерений.Вставить("Контрагент", Источник[ИмяКонтрагента]);
            ДанныеИзмерений.Вставить("ТочкаДоставки", Источник[ИмяТочкиДоставкиВПоступлении]);
            
            ДанныеРесурсов = Новый Структура;
            ДанныеРесурсов.Вставить("КоличествоНакладная", Источник.Товары.Итог("Количество"));
            ДанныеРесурсов.Вставить("СуммаНакладная", Источник.Товары.Итог("Сумма"));
            ДанныеРесурсов.Вставить("СуммаНДСНакладная", Источник.Товары.Итог("СуммаНДС"));
            
            // Если существует документ в регистре
            Для Каждого НоваяЗапись Из НаборЗаписей Цикл
                
                ЭКОМ_ПодпискаСобытий.СформироватьЗаписьВРегистр(НоваяЗапись, ДанныеИзмерений, ДанныеРесурсов);
                
            КонецЦикла;
            
            НаборЗаписей.Записать();
            
        КонецЕсли;
        
    КонецЕсли;
    
КонецЕсли;

// СчетФактураПолученный
Если ТипЗнч(Источник.Ссылка) = Тип(ТипДокументаСчетФактуры) Тогда
    
    ДанныеИзмерений = Новый Структура;
    ДанныеИзмерений.Вставить("СЧФ", Источник.Ссылка);
    
    ДанныеРесурсов = Новый Структура;
    
    Для каждого СтрокаДокументОснование Из Источник.ДокументыОснования Цикл
        Если ТипЗнч(СтрокаДокументОснование.ДокументОснование) = Тип(ТипДокументаПоступления) Тогда
            
            ДокументПоступления = СтрокаДокументОснование.ДокументОснование;
            ДокументЗаказа = ДокументПоступления[ИмяЗаказа1С];
            
            ДанныеОтбораЗаполнены = Ложь;
            НаборЗаписей = РегистрыСведений.ЭКОМ_ЦепочкиДокументовПокупателя.СоздатьНаборЗаписей();
            
            СтруктураОтбора = Новый Структура;
            СтруктураОтбора.Вставить("Заказ", ДокументЗаказа);
            СтруктураОтбора.Вставить("Накладная", ДокументПоступления);
            Для каждого Данные Из СтруктураОтбора Цикл
                Если ЗначениеЗаполнено(Данные.Значение) Тогда
                    НаборЗаписей.Отбор[Данные.Ключ].Установить(Данные.Значение);
                    ДанныеОтбораЗаполнены = Истина;
                КонецЕсли;
            КонецЦикла;
            
            // Выполним блокировку данных
            ЭКОМ_ПодпискаСобытий.ВыполнитьБлокировкуНабораЗаписей(НаборЗаписей.Отбор, "ЭКОМ_ЦепочкиДокументовПокупателя");
            Если ДанныеОтбораЗаполнены Тогда
                НаборЗаписей.Прочитать();
                // Если существует документ в регистре
                Для Каждого НоваяЗапись Из НаборЗаписей Цикл
                    
                    ДанныеРесурсов.Вставить("СуммаСЧФ", НоваяЗапись.СуммаНакладная);
                    ЭКОМ_ПодпискаСобытий.СформироватьЗаписьВРегистр(НоваяЗапись, ДанныеИзмерений, ДанныеРесурсов);
                    
                КонецЦикла;
                
                НаборЗаписей.Записать();
                
            КонецЕсли;
        КонецЕсли;
    КонецЦикла;
КонецЕсли;

// ВозвратТоваровПоставщику
Если ТипЗнч(Источник.Ссылка) = Тип(ТипДокументаВозвратПоставщику) Тогда

	// проверка документа основания и наличия соответствующего реквизита у ВозвратТоваровПоставщику
	РеквизитОснованиеСуществует = Метаданные.Документы[ИмяДокументаВозвратПоставщику].Реквизиты.Найти(ОснованиеВозвратаПоставщику) <> Неопределено;
	Если РеквизитОснованиеСуществует И ЗначениеЗаполнено(Источник[ОснованиеВозвратаПоставщику]) Тогда
		
		// отбор будет установлен на измерение в зависимости от типа значения реквизита Сделка
		ИзмерениеОтбор = "";
		Если ТипЗнч(Источник[ОснованиеВозвратаПоставщику]) = Тип(ТипДокументаЗаказа) Тогда
			ИзмерениеОтбор    = "Заказ";
		ИначеЕсли ТипЗнч(Источник[ОснованиеВозвратаПоставщику]) = Тип(ТипДокументаПоступления) Тогда
			ИзмерениеОтбор    = "Накладная";
		КонецЕсли;
		
		// для необрабатываемых типов значений действия не выполняется
		Если ИзмерениеОтбор <> "" Тогда
			
			// поиск скуществующих записей по документу основания	
			НаборЗаписей = РегистрыСведений.ЭКОМ_ЦепочкиДокументовПокупателя.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор[ИзмерениеОтбор].Установить(Источник[ОснованиеВозвратаПоставщику]);
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() Тогда
				
				ДанныеИзмерений   = Новый Структура;
				ДанныеИзмерений.Вставить("ВозвратнаяНакладная", Источник.Ссылка);
				
				ДанныеРесурсов = Новый Структура;
				Если Источник.Проведен Тогда
					ДанныеРесурсов.Вставить("КоличествоВозврат", Источник.Товары.Итог("Количество"));
					ДанныеРесурсов.Вставить("СуммаВозврат", Источник.Товары.Итог("Сумма"));
				КонецЕсли;   	
				
				ЭКОМ_ПодпискаСобытий.ВыполнитьБлокировкуНабораЗаписей(НаборЗаписей.Отбор, "ЭКОМ_ЦепочкиДокументовПокупателя"); 
				Для Каждого Запись Из НаборЗаписей Цикл
					ЭКОМ_ПодпискаСобытий.СформироватьЗаписьВРегистр(Запись, ДанныеИзмерений, ДанныеРесурсов);
				КонецЦикла;
				НаборЗаписей.Записать();     					
			КонецЕсли;// проверка наличия записей
			
		КонецЕсли; // проверка соответствия измерения отбора
	КонецЕсли;// проверка наличия реквизита и его заполненности
КонецЕсли;//проверка типа